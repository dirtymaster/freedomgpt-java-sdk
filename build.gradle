plugins {
    id 'java'
    id 'org.openapi.generator' version '7.8.0'
    id 'maven-publish'
}

group = 'dirtymaster'
version = '0.1.0'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(23)
    }
}

repositories {
    mavenCentral()
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = 'dirtymaster'
            artifactId = 'freedomgpt-java-sdk'
            version = "0.1.0"
            from components.java

            pom {
                name = 'FreedomGPT Java SDK'
            }
        }
    }
}

dependencies {
    compileOnly 'org.openapitools:openapi-generator:7.10.0' exclude group: 'org.slf4j', module: 'slf4j-simple'
    implementation 'javax.annotation:javax.annotation-api:1.3.2'
    implementation 'io.gsonfire:gson-fire:1.9.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:4.12.0'

    compileOnly 'org.projectlombok:lombok:1.18.36'
    annotationProcessor 'org.projectlombok:lombok:1.18.36'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.11.4'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.11.4'
}

tasks.named('test') {
    useJUnitPlatform()
}

sourceSets {
    main {
        java {
            srcDir "${buildDir}/generated/src/main/java"
        }
    }
}

openApiGenerate {
    generatorName = "java"
    inputSpec = "$projectDir/src/main/resources/openapi.yml".toString()
    outputDir = "$buildDir/generated".toString()
    apiPackage = "dirtymaster.fgpt.api"
    modelPackage = "dirtymaster.fgpt.model"
    configOptions = [
            disallowAdditionalPropertiesIfNotPresent: "false"
    ]}

compileJava.dependsOn tasks.openApiGenerate
